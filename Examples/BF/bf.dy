reverse =
  let go acc = \case
    | [] -> acc
    | a :: as -> go (a :: acc) as
  in
  go []
and

head (a :: _) = a and
tail (_ :: as) = as and

parse =
  let go scope stack = \case
    | [] ->
      ( case stack of
        | [] -> reverse scope
        | _ -> Exception "Ummatched '['."
      )
    | '+' :: cs -> go (Incr :: scope) stack cs
    | '-' :: cs -> go (Decr :: scope) stack cs
    | '<' :: cs -> go (MovL :: scope) stack cs
    | '>' :: cs -> go (MovR :: scope) stack cs
    | ',' :: cs -> go (Read :: scope) stack cs
    | '.' :: cs -> go (Write :: scope) stack cs
    | '[' :: cs -> go [] (scope :: stack) cs
    | ']' :: cs ->
      ( case stack of
        | [] -> Exception "Unmatched ']'."
        | parent :: stack -> go (Loop (reverse scope) :: parent) stack cs
      )
    | _ :: cs -> go scope stack cs
  in
  go [] []
and

zeros = 0 :: zeros and
tape = { left = zeros, crr = 0, right = zeros } and

wrap = \case
  | 256 -> 0
  | -1 -> 255
  | n -> n
and

evalOp op tape@{ .. } =
  case op of
  | Incr -> pure { left, crr = wrap (crr + 1), right }
  | Decr -> pure { left, crr = wrap (crr - 1), right }
  | MovL -> pure { left = tail left, crr = head left, right = crr :: right }
  | MovR -> pure { left = crr :: left, crr = head right, right = tail right }
  | Read -> getChar >>= \c. pure { left, crr = charToNum c, right }
  | Write -> putChar (numToChar crr) *> pure tape
  | Loop ops ->
      case crr of
      | 0 -> pure tape
      | _ -> evalOps ops tape >>= evalOp (Loop ops)
and

evalOps ops tape =
  case ops of
  | [] -> pure tape
  | op :: ops -> evalOp op tape >>= evalOps ops
and

eval ops = evalOps ops tape *> pure () and

main = (head <$> getArgs) >>= readFile >>= (parse; eval)
